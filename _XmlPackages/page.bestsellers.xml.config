<?xml version="1.0" encoding="utf-8" ?>
<package displayname="Best Sellers" version="2.1" debug="false" allowengine="true">

	<!-- ###################################################################################################### -->
	<!-- Copyright AspDotNetStorefront.com, 1995-2009.  All Rights Reserved.					                -->
	<!-- http://www.aspdotnetstorefront.com														                -->
	<!-- For details on this license please visit  the product homepage at the URL above.		                -->
	<!-- THE ABOVE NOTICE MUST REMAIN INTACT.                                                                   -->
	<!--                                                                                                        -->
	<!-- ###################################################################################################### -->

    <query name="Products" rowElementName="Product">
        <sql>
            <![CDATA[
                exec aspdnsf_BestSellers 180, @return, @orderby
            ]]>
        </sql>
        <queryparam paramname="@return" paramtype="appconfig" requestparamname="BestSellersN" sqlDataType="int" defvalue="10"  validationpattern="^\d{1,10}$" />
        <queryparam paramname="@orderby" paramtype="request" requestparamname="orderby" sqlDataType="int" defvalue="1"  validationpattern="^\d$" />
    </query>

    <PackageTransform>
        <xsl:stylesheet version="1.0" xmlns:xsl="http://www.w3.org/1999/XSL/Transform" xmlns:aspdnsf="urn:aspdnsf" exclude-result-prefixes="aspdnsf">
            <xsl:output method="html" omit-xml-declaration="yes" />

            <xsl:param name="LocaleSetting" select="/root/Runtime/LocaleSetting" />
            <xsl:param name="WebConfigLocaleSetting" select="/root/Runtime/WebConfigLocaleSetting" />

            <xsl:param name="ShowPics"><xsl:value-of select="aspdnsf:AppConfigBool('BestSellersShowPics')"/></xsl:param>
            <xsl:param name="ProductCount" select="count(/root/Products/Product)" />
            <xsl:param name="colspan"><xsl:choose><xsl:when test="/root/System/IsAdminUser='true' or /root/System/IsSuperUser='true'">5</xsl:when><xsl:otherwise>4</xsl:otherwise></xsl:choose></xsl:param>


            <xsl:template match="/">
                <p align="left"><xsl:value-of select="aspdnsf:StringResource('AppConfig.BestSellersIntro')" disable-output-escaping="yes" /></p>
                <xsl:choose>
                    <xsl:when test="$ProductCount &gt; 0">
                        <table width="100%" cellpadding="2" cellspacing="0" border="0">
                            <xsl:attribute name="style">border-style: solid; border-width: 0px; border-color: #<xsl:value-of select="aspdnsf:AppConfig('HeaderBGColor')" /></xsl:attribute>
                            <tr>
                                <td align="left" valign="top">
                                    <img src="{aspdnsf:LocateImageURL(concat('skins/Skin_', /root/System/SkinID, '/images/bestsellers.gif'))}" border="0" /><br/>
                                    <table width="100%" cellpadding="4" cellspacing="0" border="0">
                                        <xsl:attribute name="style"><xsl:value-of select="aspdnsf:AppConfig('BoxFrameStyle')" /></xsl:attribute>
                                        <tr>
                                            <td align="left" valign="top">
                                                <table border="0" cellpadding="0" cellspacing="1" width="100%">
                                                    <tr>
                                                        <td align="left"><b><xsl:value-of select="aspdnsf:StringResource('bestsellers.aspx.4')" disable-output-escaping="yes" /></b></td>
                                                        <td align="left"><b><xsl:value-of select="aspdnsf:StringResource('bestsellers.aspx.5')" disable-output-escaping="yes" /></b></td>
                                                        <xsl:if test="/root/System/IsAdminUser='true' or /root/System/IsSuperUser='true'">
                                                            <td align="center"><b><a href="bestsellers.aspx?orderby=units"><xsl:value-of select="aspdnsf:StringResource('bestsellers.aspx.1')" disable-output-escaping="yes" /></a></b></td>
                                                            <td align="center"><b><a href="bestsellers.aspx?orderby=amount"><xsl:value-of select="aspdnsf:StringResource('bestsellers.aspx.2')" disable-output-escaping="yes" /></a></b></td>
                                                        </xsl:if>
                                                        <td align="left"><b><xsl:value-of select="aspdnsf:StringResource('bestsellers.aspx.6')" disable-output-escaping="yes" /></b></td>
                                                        <td align="left"><b><xsl:value-of select="aspdnsf:StringResource('AppConfig.CategoryPromptSingular')" disable-output-escaping="yes" /></b></td>
                                                    </tr>
                                                    <tr><td colspan="{$colspan}" height="4" width="100%"><img src="images/spacer.gif" height="1" width="1" /></td></tr>
                                                    <xsl:apply-templates select="/root/Products/Product"></xsl:apply-templates>
                                                </table>
                                            </td>
                                        </tr>
                                        <tr><td colspan="{$colspan}">&#0160;</td></tr>
                                    </table>
                                </td>
                            </tr>
                        </table>
                    </xsl:when>
                    <xsl:otherwise>
                        <p align="left"><b><xsl:value-of select="aspdnsf:StringResource('bestsellers.aspx.3')" disable-output-escaping="yes" /></b></p>
                    </xsl:otherwise>
                </xsl:choose>

            </xsl:template>



            <xsl:template match="Product">
                <xsl:param name="pName" select="aspdnsf:GetMLValue(ProductName)" />
                <xsl:param name="vName" select="aspdnsf:GetMLValue(VariantName)" />
                <xsl:param name="ProductName"><xsl:value-of select="$pName" /><xsl:if test="$vName!=''"><xsl:value-of select="concat('-', $vName)" /></xsl:if></xsl:param>
                <xsl:param name="ProductSKU"><xsl:value-of select="SKU" /><xsl:if test="SKUSuffix!=''"><xsl:value-of select="SKUSuffix" /></xsl:if></xsl:param>

                <tr>
                    <td align="left" valign="middle"><xsl:value-of select="position()" />.</td>
                    <td align="left" valign="middle">

                      <xsl:if test="$ShowPics='true'">
                        <!-- 
                          Since we're displaying individual variants as line items, we should honor imagefilenameoverride per variant
                          If variant has image override defined, use that otherwise fall back to the main product's image filename override if present
                          Otherwise, default rule lookup is used which is via ProductID or VariantD
                        -->
                        
                        <xsl:choose>
                          <xsl:when test="VariantCount > 1">
                            <xsl:variable name="img" select="aspdnsf:LookupVariantImage(ProductID, VariantID, VariantImageFilenameOverride, SKU, 'icon', 0, '')" />
                            <xsl:value-of select="aspdnsf:ProductLink(ProductID, SEName, 1, $img)" disable-output-escaping="yes" />&#0160;
                          </xsl:when>
                          <xsl:otherwise>
                            <xsl:variable name="img" select="aspdnsf:LookupProductImage(ProductID, ImageFilenameOverride, SKU, 'icon', 1, '')" />
                            <xsl:value-of select="aspdnsf:ProductLink(ProductID, SEName, 1, $img)" disable-output-escaping="yes" />&#0160;
                          </xsl:otherwise>
                        </xsl:choose>
                      </xsl:if>

                      <xsl:value-of select="aspdnsf:ProductLink(ProductID, SEName, 1, $ProductName)" disable-output-escaping="yes" />
                     
                    </td>
                    <xsl:if test="/root/System/IsAdminUser='true' or /root/System/IsSuperUser='true'">
                        <td align="left" valign="middle"><xsl:value-of select="aspdnsf:FormatDecimal(SalesCount, 2)" /></td>
                        <td align="left" valign="middle"><xsl:value-of select="aspdnsf:FormatCurrency(SalesDollars)" disable-output-escaping="yes" /></td>
                    </xsl:if>
                    <td align="left" valign="middle"><xsl:value-of select="$ProductSKU" /></td>
                    <td align="left" valign="middle"><xsl:value-of select="aspdnsf:ProductEntityList(ProductID, 'category')" disable-output-escaping="yes" /></td>
                </tr>
                <tr><td colspan="{$colspan}" height="1" width="100%" class="LightCell"><img src="images/spacer.gif" height="1" width="1" /></td></tr>


            </xsl:template>


        </xsl:stylesheet>
    </PackageTransform>
</package>
